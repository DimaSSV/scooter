
#Область ПрограммныйИнтерфейс

// Функция - Создаёт объект внешней обработки для произвольного алгоритма
//
// Параметры:
//  Ссылка	 - СправочникСсылка.скт_ПроизвольныеАлгоритмы - Ссылка на алгоритм, обработку которого необходимо создать
// 
// Возвращаемое значение:
//  ОбработкаОбъект, ВнешняяОбработкаОбъект - Созданная обработка произвольного алгоритма
//
Функция ОбъектВнешнейОбработки(Ссылка) Экспорт

	Если Ссылка.Расположение = ПредопределенноеЗначение("Перечисление.скт_ВариантыРасположенияАлгоритмов.ВнешнийФайл") Тогда
		// Подключение
		ИмяОбработки = ПодключитьВнешнююОбработку(Ссылка);
		
		// Проверка корректности переданных параметров.
		Если ИмяОбработки = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		Возврат ВнешниеОбработки.Создать(ИмяОбработки);
		
	Иначе
		
		Возврат Обработки[Ссылка.ИмяВстроеннойОбработки].Создать();
		
	КонецЕсли;

КонецФункции // ОбъектВнешнейОбработки()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПодключитьВнешнююОбработку(Ссылка)

#Если Клиент Тогда
	ПутьКФайлу = Ссылка.ПутьКФайлу;
	Если Ссылка.ПодключатьсяОтладчиком И ЗначениеЗаполнено(ПутьКФайлу) Тогда
		Файл = Новый Файл(ПутьКФайлу);
		Если Файл.Существует() Тогда
			Возврат Файл.ПолноеИмя;
		КонецЕсли;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.КлючДанных = Ссылка;
		Сообщение.Поле = "ПутьКФайлу";
		Сообщение.ПутьКДанным = "Объект";
		Сообщение.Текст = 
			СтрШаблон("Для обработчика ""%1"" заполнен путь для отладки, но файл не найден. " + 
			"Отладка обработчика не доступна", Ссылка);
		Сообщение.Сообщить();
	КонецЕсли;
	
#КонецЕсли
	
#Если ТолстыйКлиентОбычноеПриложение Тогда
	ИмяОбработки = ПолучитьИмяВременногоФайла();
	ХранилищеОбработки = Ссылка.ХранилищеФайла;
	ДвоичныеДанные = ХранилищеОбработки.Получить();
	ДвоичныеДанные.Записать(ИмяОбработки);
	Возврат ИмяОбработки;
#КонецЕсли

	АдресВоВременномХранилище = ПоместитьВоВременноеХранилище(Ссылка.ХранилищеФайла.Получить());
	ИмяОбработки = ВнешниеОбработки.Подключить(АдресВоВременномХранилище, , Ложь);
	Возврат ИмяОбработки;

КонецФункции // ПодключитьВнешнююОбработку()

#КонецОбласти