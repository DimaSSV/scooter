
#Область ПрограммныйИнтерфейс

#Если Не ТонкийКлиент И Не ВебКлиент Тогда

// Функция - Установить схему компоновки в форме. Вариант для работы из Толстого клиента
//
// Параметры:
//  Форма							 - ФормаКлиентскогоПриложения - Форма
//				, в которой необходимо инициировать компоновщик настроек
//  СхемаКомпоновки					 - СхемаКомпоновкиДанных - Схема, которая будет использована для инициализации компоновщика
//  АдресСхемыКомпоновки			 - Строка - Адрес, по которому будет помещена схема (переменная будет заполнена)
//  КомпоновщикНастроек				 - КомпоновщикНастроекСхемыКомпоновкиДанных - Компоновщик
//			, который необходимо инициализировать
//  ПриОшибкеЗагружатьПустуюСхему	 - Булево - Если не удалось инициализировать компоновщик переданной схемой 
//			- в него будет загружена пустая схема
//  УдалятьНедоступныеНастройки		 - Булево - Способ восстановления настроек в компоновщике, после инициализации
// 
// Возвращаемое значение:
//  Булево - Истина, если схема успешно установлена
//
Функция УстановитьСхемуКомпоновкиВФорме(Форма, СхемаКомпоновки, АдресСхемыКомпоновки, 
	КомпоновщикНастроек = Неопределено, ПриОшибкеЗагружатьПустуюСхему = Истина, УдалятьНедоступныеНастройки = Ложь) Экспорт 
	
	Если КомпоновщикНастроек = Неопределено Тогда
		КомпоновщикНастроек = Форма.Компоновщик;
	КонецЕсли;
	Если ЭтоАдресВременногоХранилища(АдресСхемыКомпоновки) Тогда
		ПоместитьВоВременноеХранилище(СхемаКомпоновки, АдресСхемыКомпоновки);
	Иначе
		АдресСхемыКомпоновки = ПоместитьВоВременноеХранилище(СхемаКомпоновки, Форма.УникальныйИдентификатор);
	КонецЕсли;
	Попытка
		КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпоновки));
		Успех = Истина;
	Исключение
		Если ПриОшибкеЗагружатьПустуюСхему Тогда
			Если ЭтоАдресВременногоХранилища(АдресСхемыКомпоновки) Тогда
				ПоместитьВоВременноеХранилище(Новый СхемаКомпоновкиДанных, АдресСхемыКомпоновки);
			Иначе
				АдресСхемыКомпоновки = ПоместитьВоВременноеХранилище(Новый СхемаКомпоновкиДанных, Форма.УникальныйИдентификатор);
			КонецЕсли;
			КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпоновки));
		КонецЕсли; 
		Успех = Ложь;
	КонецПопытки;
	Если УдалятьНедоступныеНастройки Тогда
		СпособВосстановления = СпособВосстановленияНастроекКомпоновкиДанных.ПроверятьДоступность;
	Иначе
		СпособВосстановления = СпособВосстановленияНастроекКомпоновкиДанных.Полное;
	КонецЕсли; 
	КомпоновщикНастроек.Восстановить(СпособВосстановления);
	Возврат Успех;
	
КонецФункции

#КонецЕсли

#КонецОбласти
