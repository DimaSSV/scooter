
#Область ПрограммныйИнтерфейс

// Возвращает Истина, если функциональная подсистема существует в конфигурации.
// Предназначена для реализации вызова необязательной подсистемы (условного вызова).
// У функциональной подсистемы снят флажок "Включать в командный интерфейс".
// См. также ОбщегоНазначенияПереопределяемый.ПриОпределенииОтключенныхПодсистем
// и ОбщегоНазначенияКлиент.ПодсистемаСуществует для вызова из клиентского кода.
//
// Параметры:
//  ПолноеИмяПодсистемы - Строка - полное имя объекта метаданных подсистема
//                        без слов "Подсистема." и с учетом регистра символов.
//                        Например: "СтандартныеПодсистемы.ВариантыОтчетов".
//
// Пример:
//  Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ВариантыОтчетов") Тогда
//  	МодульВариантыОтчетов = ОбщегоНазначения.ОбщийМодуль("ВариантыОтчетов");
//  	МодульВариантыОтчетов.<Имя метода>();
//  КонецЕсли;
//
// Возвращаемое значение:
//  Булево - Истина, если подсистема существует.
//
Функция ПодсистемаСуществует(ПолноеИмяПодсистемы) Экспорт
	
	ИменаПодсистем = скт_ОбщегоНазначенияПовтИсп.ИменаПодсистем();
	Возврат ИменаПодсистем.Получить(ПолноеИмяПодсистемы) <> Неопределено;
	
КонецФункции

Функция ВалидноеИмяДляПеременной(Знач Текст) Экспорт

	Результат = СокрЛП(Текст);
	
	// уберём лишние символы
	Результат = СтрЗаменить(Результат, " ", "_");
	Результат = СтрЗаменить(Результат, ".", "_");
	Результат = СтрЗаменить(Результат, "(", "_");
	Результат = СтрЗаменить(Результат, ")", "_");
	Результат = СтрЗаменить(Результат, Символы.НПП, "_");
	Результат = СтрЗаменить(Результат, Символы.ВТаб, "_");
	Результат = СтрЗаменить(Результат, Символы.Таб, "_");
	Результат = СтрЗаменить(Результат, Символы.ПС, "_");
	Результат = СтрЗаменить(Результат, Символы.ПФ, "_");
	Результат = СтрЗаменить(Результат, Символы.ВК, "_");
	Результат = СтрЗаменить(Результат, "№", "");
	Результат = СтрЗаменить(Результат, "/", "_");
	Результат = СтрЗаменить(Результат, "\", "_");
	
	// Проверим - не начинается ли имя с числа
	Пока СтрДлина(Результат) Цикл
		КодПервогоСимвола = КодСимвола(Лев(Результат,1));
		Если КодПервогоСимвола > 65 Тогда
			Прервать;
		КонецЕсли;
		Результат = Прав(Результат, СтрДлина(Результат)-1);
	КонецЦикла;
	
	Если Не СтрДлина(Результат) Тогда
		Возврат "Имя";
	КонецЕсли;
	
	Возврат Результат;

КонецФункции // ВалидноеИмяДляПеременной()

// Выполняет сериализацию объекта в XML
//
// Параметры:
//  Значение  - Произвольный - объект, который необходимо сериализовать в XML.
//
// Возвращаемое значение:
//   Строка   - объект, сериализованный в XML.
//
Функция ПолучитьXML(Значение) Экспорт
	
	Запись = Новый ЗаписьXML();
	Запись.УстановитьСтроку();
	СериализаторXDTO.ЗаписатьXML(Запись, Значение);
	Возврат Запись.Закрыть();
	
КонецФункции

Функция ПолучитьОбъектПоXML(XMLСтрока) Экспорт

	Чтение = Новый ЧтениеXML();
	Чтение.УстановитьСтроку(XMLСтрока);
	Возврат СериализаторXDTO.ПрочитатьXML(Чтение);

КонецФункции // ПолучитьОбъектПоXML()

Процедура ДвоичныеДанныеТабличногоДокумента(ТабличныйДокумент, ТипФайла, Поток) Экспорт

	ТабличныйДокумент.Записать(Поток, ТипФайла);
	
КонецПроцедуры // ДвоичныеДанныеТабличногоДокумента()

Процедура CSVПоТаблицеЗначений(Таблица, Поток) Экспорт

	Запись = Новый ЗаписьТекста(Поток, КодировкаТекста.UTF8, Символы.ПС, Символы.ПС, Истина);
	ЧастиСтроки = Новый Массив;
	Для Каждого текКолонка Из Таблица.Колонки Цикл
		ИмяСвойства = ?(ПустаяСтрока(текКолонка.Заголовок), текКолонка.Имя, текКолонка.Заголовок);
		ИмяСвойства = ВалидноеИмяДляПеременной(ИмяСвойства);
		ЧастиСтроки.Добавить(ИмяСвойства);
	КонецЦикла;
	Запись.ЗаписатьСтроку(СтрСоединить(ЧастиСтроки, ";") + ";");
	
	Для Каждого текСтрока Из Таблица Цикл
		ЧастиСтроки.Очистить();
		Для Каждого текКолонка Из Таблица.Колонки Цикл
			Если ТипЗнч(текСтрока[текКолонка.Имя]) = Тип("Дата") Тогда 
				Значение = ЗаписатьДатуJSON(текСтрока[текКолонка.Имя], ФорматДатыJSON.ISO)
			Иначе
				Значение = текСтрока[текКолонка.Имя];
			КонецЕсли;
			ЧастиСтроки.Добавить(Значение);
		КонецЦикла;
		Запись.ЗаписатьСтроку(СтрСоединить(ЧастиСтроки, ";")+ ";");
	КонецЦикла;
	Запись.Закрыть();
	
КонецПроцедуры // CSVПоТаблицеЗначений()

Процедура JSONПоТаблицеЗначений(Таблица, Поток) Экспорт

	Запись = Новый ЗаписьJSON;
	ПараметрыЗаписи = Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Нет);
	Запись.ОткрытьПоток(Поток, КодировкаТекста.UTF8, Ложь, ПараметрыЗаписи);
	Запись.ЗаписатьНачалоМассива();
	Для Каждого текСтрока Из Таблица Цикл
		Запись.ЗаписатьНачалоОбъекта();
		Для Каждого текКолонка Из Таблица.Колонки Цикл
			ИмяСвойства = ?(ПустаяСтрока(текКолонка.Заголовок), текКолонка.Имя, текКолонка.Заголовок);
			ИмяСвойства = ВалидноеИмяДляПеременной(ИмяСвойства);
			Запись.ЗаписатьИмяСвойства(ИмяСвойства);
			Значение = текСтрока[текКолонка.Имя];
			Если ТипЗнч(Значение) = Тип("Строка")
				Или ТипЗнч(Значение) = Тип("Число")
				Или ТипЗнч(Значение) = Тип("Булево")
				Или Значение = Неопределено Тогда
				Запись.ЗаписатьЗначение(Значение);
			ИначеЕсли ТипЗнч(Значение) = Тип("Дата") Тогда
				Запись.ЗаписатьЗначение(ЗаписатьДатуJSON(Значение, ФорматДатыJSON.ISO));
			Иначе
				Запись.ЗаписатьЗначение(Строка(Значение));
			КонецЕсли;	
		КонецЦикла;
		Запись.ЗаписатьКонецОбъекта();
	КонецЦикла;
	Запись.ЗаписатьКонецМассива();
	Запись.Закрыть();
	
КонецПроцедуры // JSONПоТаблицаЗначений()

Процедура XMLПоТаблицеЗначений(Таблица, Поток) Экспорт

	Запись = Новый ЗаписьXML;
	Запись.Отступ = Ложь;
	Запись.ОткрытьПоток(Поток, КодировкаТекста.UTF8, Ложь);
	Запись.ЗаписатьОбъявлениеXML();
	Запись.ЗаписатьНачалоЭлемента("Message");
	Для Каждого текСтрока Из Таблица Цикл
		Запись.ЗаписатьНачалоЭлемента("Record");
		Для Каждого текКолонка Из Таблица.Колонки Цикл
			ИмяСвойства = ?(ПустаяСтрока(текКолонка.Заголовок), текКолонка.Имя, текКолонка.Заголовок);
			ИмяСвойства = ВалидноеИмяДляПеременной(ИмяСвойства);
			Значение = текСтрока[текКолонка.Имя];
			Если ТипЗнч(Значение) = Тип("Строка") Тогда
				Запись.ЗаписатьАтрибут(ИмяСвойства, Значение);
			ИначеЕсли ТипЗнч(Значение) = Тип("Число") Тогда
				Запись.ЗаписатьАтрибут(ИмяСвойства, Формат(Значение, "ЧН=0; ЧГ=0"));
			ИначеЕсли ТипЗнч(Значение) = Тип("Булево") Тогда
				Запись.ЗаписатьАтрибут(ИмяСвойства, ?(Значение, "true", "false"));
			ИначеЕсли ТипЗнч(Значение) = Тип("Дата") Тогда
				Запись.ЗаписатьАтрибут(ИмяСвойства, ЗаписатьДатуJSON(Значение, ФорматДатыJSON.ISO));
			Иначе
				Запись.ЗаписатьАтрибут(ИмяСвойства, Строка(Значение));
			КонецЕсли;	
		КонецЦикла;
		Запись.ЗаписатьКонецЭлемента();
	КонецЦикла;
	Запись.ЗаписатьКонецЭлемента();
	Запись.Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗаписатьВЖурнал(вИмяСобытия, вКомментарий, вУровеньЖурнала = "Информация", вДанные = Неопределено, вМетаданные = Неопределено) Экспорт
	Уровень = УровеньЖурналаРегистрации[вУровеньЖурнала];
	ЗаписьЖурналаРегистрации(СтрШаблон("Самокат.%1", вИмяСобытия), Уровень, вМетаданные, вДанные, вКомментарий);	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
// Код процедур и функций
#КонецОбласти