
#Область ОписаниеПеременных

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ТекущийПуть = Параметры.ТекущийПуть;
	Если ПустаяСтрока(ТекущийПуть) Тогда
		ТекущийПуть = КаталогВременныхФайлов();
	КонецЕсли;
	ЗаполнитьДоступнуюИерархию();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НавигацияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	текСтрока = Навигация.НайтиПоИдентификатору(ВыбраннаяСтрока);
	Если Не текСтрока.ЭтоКаталог Тогда
		Возврат;
	КонецЕсли;
	
	НавигацияНаСервере(ВыбраннаяСтрока);
	ПодключитьОбработчикОжидания("ОбновитьФокус", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьФокус()

	ТекущийЭлемент = Элементы.Навигация;

КонецПроцедуры // ОбновитьФокус()


#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыы
// Код процедур и функций
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаВыбор(Команда)
	
	Если Параметры.ВыборКаталога Тогда
		Закрыть(ТекущийПуть);
	ИначеЕсли Элементы.Навигация.ТекущаяСтрока = Неопределено Тогда
		Закрыть(Неопределено);
	Иначе
		ВыбраннаяСтрока = Навигация.НайтиПоИдентификатору(Элементы.Навигация.ТекущаяСтрока);
		Закрыть(ТекущийПуть + ВыбраннаяСтрока.Имя);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьДоступнуюИерархию()

	Элементы.Навигация.Заголовок = ТекущийПуть;
	Навигация.Очистить();
	СтрокаНазад = ДобавитьЭлементНавигации("..", Истина);
	ДоступныеФайлы = НайтиФайлы(ТекущийПуть, "*");
	Для Каждого текФайл Из ДоступныеФайлы Цикл
		ДобавитьЭлементНавигации(текФайл.Имя, текФайл.ЭтоКаталог());
	КонецЦикла;
	Навигация.Сортировать("ЭтоКаталог Убыв, Имя", Новый СравнениеЗначений);
	текИндекс = Навигация.Индекс(СтрокаНазад);
	Навигация.Сдвинуть(текИндекс, -текИндекс);

КонецПроцедуры // ЗаполнитьДоступнуюИерархию()

&НаСервере
Функция ДобавитьЭлементНавигации(Имя, ЭтоКаталог = Ложь)

	Если Параметры.ВыборКаталога И Не ЭтоКаталог Тогда
		Возврат Неопределено;
	КонецЕсли;
	НоваяСтрока = Навигация.Добавить();
	НоваяСтрока.Имя = Имя;
	НоваяСтрока.ЭтоКаталог = ЭтоКаталог;
	Возврат НоваяСтрока;

КонецФункции // ДобавитьЭлементНавигации()

&НаСервере
Процедура НавигацияНаСервере(ВыбраннаяСтрока)

	ВыбраннаяСтрокаДанные = Навигация.НайтиПоИдентификатору(ВыбраннаяСтрока);
	ЦелевойКаталог = Новый Файл(ТекущийПуть + ВыбраннаяСтрокаДанные.Имя);
	Если Не ЦелевойКаталог.ЭтоКаталог() Тогда
		Возврат;
	КонецЕсли;
	ТекущийПуть = ЦелевойКаталог.ПолноеИмя + ПолучитьРазделительПути();
	ЗаполнитьДоступнуюИерархию();

КонецПроцедуры // НавигацияНаСервере()





#КонецОбласти