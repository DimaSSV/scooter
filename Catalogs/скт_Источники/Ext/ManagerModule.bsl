#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Функция - Создаёт и инициализирует компоновщик данных переданным источником данных.
//
// Параметры:
//  СсылкаОбъект				 - СправочникСсылка.скт_Источники - Ссылка на источник данных
//  ПользовательскиеНастройки	 - ПользовательскиеНастройкиКомпоновкиДанных - Пользовательские настройки,
//			которые будут загружены в компоновщик
// 
// Возвращаемое значение:
//  КомпоновщикНастроекКомпоновкиДанных - Проинициализированный компонощик
//
Функция КомпоновщикНастроек(Знач СсылкаОбъект, ПользовательскиеНастройки = Неопределено) Экспорт

	Схема = СсылкаОбъект.Схема.Получить();
	Настройки = СсылкаОбъект.Настройки.Получить();
	
	Компоновщик = Новый КомпоновщикНастроекКомпоновкиДанных;
	Компоновщик.ЗагрузитьНастройки(Настройки);
	Если ПользовательскиеНастройки <> Неопределено Тогда
		Компоновщик.ЗагрузитьПользовательскиеНастройки(ПользовательскиеНастройки);
	КонецЕсли;
	Компоновщик.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(Схема));
	Компоновщик.Восстановить();
	
	Возврат Компоновщик;

КонецФункции // КомпоновщикНастроек()

// Функция - Выполняет компоновку указанного источника данных
//
// Параметры:
//  СсылкаОбъект			 - СправочникСсылка.скт_Источники - Ссылка на 
//			источник данных, из которого необходимо получить данные
//  Настройки				 - НастройкиКомпоновкиДанных - Настройки, для компоновки макета компоновки данных
//  ПриемникВывода			 - ТабличныйДокумент, ТаблицаЗначений, ДеревоЗначение - Объект,
//							в который будет выведен результат компоновки. По умолчанию ТабличныйДокумент
//  ВнешниеНаборы			 - Структура - Ключ = Имя внешнего набора, Значение = ТаблицаЗначений
//  ОтключитьОбщиеИтоги		 - Булево - Если включить, то вывод общих итогов будет отключен
//  МенеджерВременныхТаблиц	 - МенеджерВременныхТаблиц - Будет передан в процессор компоновки при инициализации
// 
// Возвращаемое значение:
//  ТабличныйДокумент, ТаблицаЗначений, ДеревоЗначение - Заполненный объект из аргумента ПриемникВывода
//
Функция Скомпоновать(Знач СсылкаОбъект, Знач Настройки = Неопределено, ПриемникВывода = Неопределено,
	ВнешниеНаборы = Неопределено, ОтключитьОбщиеИтоги = Истина, МенеджерВременныхТаблиц = Неопределено) Экспорт

	Схема = СсылкаОбъект.Схема.Получить();
	Если Настройки = Неопределено Тогда
		Настройки = СсылкаОбъект.Настройки.Получить();
	КонецЕсли;
	Возврат скт_КомпоновкаДанныхСервер.Скомпоновать(Схема, Настройки, ПриемникВывода, 
		ВнешниеНаборы, ОтключитьОбщиеИтоги, МенеджерВременныхТаблиц);

КонецФункции

#КонецОбласти

#КонецЕсли