
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("Метод", 		ПараметрКоманды);
	ДопПараметры.Вставить("ИДФормы", 	ПараметрыВыполненияКоманды.Источник.УникальныйИдентификатор); 
	Обработчик = Новый ОписаниеОповещения("ПослеЗаполненияПараметров", ЭтотОбъект, ДопПараметры);
	ОткрытьФорму("Справочник.скт_Методы.Форма.ФормаВызоваМетода", ДопПараметры, ПараметрыВыполненияКоманды.Источник,ПараметрыВыполненияКоманды.Уникальность,,ПараметрыВыполненияКоманды.НавигационнаяСсылка,Обработчик);
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаполненияПараметров(ПараметрыМетода, ДопПараметры) Экспорт

	Если ПараметрыМетода = Неопределено Тогда
		Возврат;
	КонецЕсли;
	РасширениеФайла = Неопределено;
	СпособДоставки = Неопределено;
	#Если ТолстыйКлиентОбычноеПриложение Или ТолстыйКлиентУправляемоеПриложение Тогда
	Адрес = ВызватьКлиент(ДопПараметры.Метод, ПараметрыМетода, РасширениеФайла, ДопПараметры.ИДФормы, СпособДоставки);
	#Иначе
	Адрес = Вызвать(ДопПараметры.Метод, ПараметрыМетода, РасширениеФайла, ДопПараметры.ИДФормы, СпособДоставки);
	#КонецЕсли
	Если СпособДоставки = ПредопределенноеЗначение("Перечисление.скт_СпособыДоставкиРезультата.ВыгрузкаВКаталог") Тогда
		Сообщить(ПолучитьСтрокуИзДвоичныхДанных(ПолучитьИзВременногоХранилища(Адрес)));	
	Иначе
		ПараметрыДиалога = Новый ПараметрыДиалогаПолученияФайлов;
		ПараметрыДиалога.Заголовок = "Сохранить результат метода";
		ПараметрыДиалога.ВыборКаталога = Ложь;
		ИмяФайла = СтрШаблон("%1.%2", СокрЛП(ДопПараметры.Метод), РасширениеФайла);
		ПолучитьФайлССервераАсинх(Адрес, ИмяФайла, ПараметрыДиалога); 
	КонецЕсли;
	
КонецПроцедуры // ПослеЗаполненияПараметров()

&НаСервере
Функция Вызвать(Метод, Параметры, РасширениеФайла, ИДФормы, СпособДоставки)

	Поток = Новый ПотокВПамяти;
	СпособДоставки = Метод.СпособДоставки;
	Справочники.скт_Методы.Вызвать(Метод, Поток, Параметры, РасширениеФайла);
	Возврат ПоместитьВоВременноеХранилище(Поток.ЗакрытьИПолучитьДвоичныеДанные(), ИДФормы);
	
КонецФункции // Вызвать()

#Если ТолстыйКлиентОбычноеПриложение Или ТолстыйКлиентУправляемоеПриложение Тогда

&НаКлиенте
Функция ВызватьКлиент(Метод, Параметры, РасширениеФайла, ИДФормы, СпособДоставки)

	Поток = Новый ПотокВПамяти;
	СпособДоставки = Метод.СпособДоставки;
	Справочники.скт_Методы.Вызвать(Метод, Поток, Параметры, РасширениеФайла);
	Возврат ПоместитьВоВременноеХранилище(Поток.ЗакрытьИПолучитьДвоичныеДанные(), ИДФормы);
	
КонецФункции // Вызвать()

#КонецЕсли