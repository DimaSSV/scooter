#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ЗаписатьНастройку(Ссылка, Значение) Экспорт

	СохраняемоеЗначение = Ссылка.ТипЗначения.ПривестиЗначение(Значение);
	
	Набор = СоздатьНаборЗаписей();
	Набор.Отбор.Настройка.Установить(Ссылка);
	
	Запись = Набор.Добавить();
	Запись.Настройка = Ссылка;
	Запись.Значение = СохраняемоеЗначение;
	
	Набор.Записать(Истина);

КонецФункции // УстановитьЗначение()

Функция ПрочитатьНастройку(Ссылка) Экспорт

	ЗначениеВБазе = Неопределено;
	
	Запись = СоздатьМенеджерЗаписи();
	Запись.Настройка = Ссылка;
	Запись.Прочитать();
	Если Запись.Выбран() Тогда
		ЗначениеВБазе = Запись.Значение;
	КонецЕсли;
	
	Возврат Ссылка.ТипЗначения.ПривестиЗначение(ЗначениеВБазе);

КонецФункции // ПолучитьЗначение()

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ВызватьИсключение НСтр("ru = 'Значения настроек не предназначены для просмотра или редактирования.';
							|en = ''");
КонецПроцедуры

#КонецОбласти

#КонецЕсли